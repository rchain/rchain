// A set of testnet admin-controlled 'genesis vault'-s will be our approach for providing
// a testnet-token supply for the Testnet 1.
// Everything regarding the genesis vaults is subject to change.

new
  rl(`rho:registry:lookup`), RevVaultCh,
  vaultCh,
  stdout(`rho:io:stdout`)
in {

  rl!(`rho:id:1o93uitkrjfubh43jt19owanuezhntag5wh74c6ur5feuotpi73q8z`, *RevVaultCh) |
  for (@(_, RevVault) <- RevVaultCh) {

    stdout!(("0.create_genesis_vault.rho")) |

    match "%REV_ADDR" {
      revAddress => {

        stdout!(("Using RevAddress:", revAddress, "to obtain a `deployer's-revAddress-bound` vault")) |

        // so far, this call is not secured. This will be changed. Most likely, the method will disappear.
        @RevVault!("findOrCreateGenesisVault", revAddress, 9000, *vaultCh ) |

        // most Vault methods return an `Either[String, A] = (false, String) \/ (true, A)`
        for (@(true, vault) <- vaultCh) {

          stdout!(("Genesis vault created")) |

          new balanceCh in {
            @vault!("balance", *balanceCh) |
            for (@balance <- balanceCh) {
              stdout!(("Genesis vault balance is", balance))
            }
          }
        }
      }
    }
  }

}


