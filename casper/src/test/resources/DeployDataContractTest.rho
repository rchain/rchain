new
  rl(`rho:registry:lookup`), RhoSpecCh,
  getDeployData(`rho:deploy:params`),
  setDeployData(`rho:test:deploy:set`),
  stdlog(`rho:io:stdlog`),
  test_get_after_set
in {
  rl!(`rho:id:zphjgsfy13h1k85isc8rtwtgt3t9zzt5pjd5ihykfmyapfc4wt3x5h`, *RhoSpecCh) |
  for(@(_, RhoSpec) <- RhoSpecCh) {
    @RhoSpec!("testSuite",
      [
        ("Get after set returns the new value", *test_get_after_set)
      ])
  } |

  contract test_get_after_set(rhoSpec, _, ackCh) = {
    new retCh in {
      getDeployData!( *retCh) |
      for (@_,@_,@originalUserId, @_ <- retCh) {
        setDeployData!("userId", "00".hexToBytes(), *retCh) |
        for (_ <- retCh) {
          getDeployData!(*retCh) |
          for (@_,@_,@newUserId,@_ <- retCh) {
            rhoSpec!("assertMany",
              [
                ((Nil, "==", originalUserId), "the userId should be Nil by default"),
                (("00".hexToBytes(), "==", newUserId), "the new value should be equal to the set value"),
              ], *ackCh)
          }
        }
      }
    }
  }
}
