//scalapackage coop.rchain.rholang.unittest

//Registry info:
//  sk: adf1e344dd34979255aeaaf5703a586fba52246d9940ef58572572db339316d3
//  pk: 4ae94eb0b2d7df529f7ae68863221d5adda402fc54303a3d90a8a7a279326828
//  user == pk
//  timestamp: 1539808849271
//Resulting unforgable name: RhoSpec == Unforgeable(0xbc4bad752f043b9e488ddfc1c69370a89700f4852f318a79d0b50d88f44e1210)
//  ==> signature data == 2a3eaa013b0a0d2a0b10feffffffffffffffff010a2a5a280a243a220a20bc4bad752f043b9e488ddfc1c69370a89700f4852f318a79d0b50d88f44e12101001
//  ==> signature == 07aa40b3bcb61b91816d7f7763ed3b7abccfeb99688ecec75cd76099f1552010bc523980fb476c990b982a46a9f36649097b8c02b590ae72a2bac4a7a790490b
//URI derived from pk == `rho:id:6wnujzcraztjfg941skrtbdkdgbko8nuaqihuhn15s66oz8ro5gwbb`
new
  RhoSpec,
  rs(`rho:registry:insertSigned:ed25519`),
  rl(`rho:registry:lookup`),
  stdlog(`rho:io:stdlog`),
  ListOpsCh,
  assert(`rho:test:assertAck`),
  testSuiteCompleted(`rho:test:testSuiteCompleted`),
  uriOut
in {
  stdlog!("debug", "Loading RhoSpec") |
  rl!(`rho:id:dputnspi15oxxnyymjrpu7rkaok3bjkiwq84z7cqcrx4ktqfpyapn4`, *ListOpsCh) |
  for(@(_, ListOps) <- ListOpsCh) {
    contract RhoSpec(@"testSuite", setup, @tests) = {
      stdlog!("debug", "Defining the testSuite") |
      new runTest, runTestOnce, retCh in {
        contract runTestOnce(@(testName, testBody), @attempt, testAckCh) = {
          stdlog!("debug", "Running test: " ++ testName ++ " attempt " + attempt) |
          new rhoSpecImpl, privateAssert in {
            contract privateAssert(@assertion, @clue, ackCh) = {
              stdlog!("debug", "asserting: " ++ clue) |
              match assertion {
                ("_ == <-", actualCh) => {
                  for (_ <- @actualCh) {
                    stdlog!("info", {"some value was received from channel"}) |
                    assert!(testName, attempt, true, clue, *ackCh)
                  }
                }
                (expected, "== <-", actualCh) => {
                  for (@actual <- @actualCh) {
                    stdlog!("info", {"actual": actual, "expected": expected}) |
                    assert!(testName, attempt, (expected, "==", actual), clue, *ackCh)
                  }
                }
                (unexpected, "!= <-", actualCh) => {
                  for (@actual <- @actualCh) {
                    stdlog!("info", {"actual": actual, "unexpected": unexpected}) |
                    assert!(testName, attempt, (unexpected, "!=", actual), clue, *ackCh)
                  }
                }
                assertion => {
                  assert!(testName, attempt, assertion, clue, *ackCh)
                }
              }
            } |

            contract rhoSpecImpl(@"assert", @assertion, @clue, ackCh) = {
              privateAssert!(assertion, clue, *ackCh)
            } |

            contract rhoSpecImpl(@"assertMany", @assertions, ackCh) = {
              match assertions {
                [] => { ackCh!(true) }

                [(assertion, clue) ...tail] => {

                  new privateAck in {
                    privateAssert!(assertion, clue, *privateAck) |

                    for (@v <- privateAck) {
                      if (v) {
                        rhoSpecImpl!("assertMany", tail, *ackCh)
                      }
                      else {
                        ackCh!(false)
                      }
                    }
                  }
                }
              }
            } |

            new setupCh in {
              setup!(bundle+{*setupCh}) |
              for (setupResult <- setupCh) {
                @testBody ! (*rhoSpecImpl, *setupResult, *testAckCh)
              }
            }
          }
        } |


        contract runTest(@(testName, testBody), ackCh) = {
          stdlog!("info", "Running test: " ++ testName) |
          runTest!((testName, testBody, 1), *ackCh)
        } |

        contract runTest(@(testName, testBody, attempts), ackCh) = {
          stdlog!("info", "Calling runTest") |

          new retCh, runTestImpl in {
            contract runTestImpl(@attempt, retCh) = {
              runTestOnce!((testName, testBody), attempt, *retCh)
            } |

            @ListOps!("range", 1, attempts + 1, *retCh) |
            for(@range <- retCh) {
              @ListOps!("foreach", range, *runTestImpl, *ackCh)
            }
          }
        } |

        @ListOps!("foreach", tests, *runTest, *retCh) |
        for (@v <- retCh) {
          testSuiteCompleted!(true)
        }
      }
    }
  } |

  rs!(
    "4ae94eb0b2d7df529f7ae68863221d5adda402fc54303a3d90a8a7a279326828".hexToBytes(),
    (9223372036854775807, bundle+{*RhoSpec}),
    "07aa40b3bcb61b91816d7f7763ed3b7abccfeb99688ecec75cd76099f1552010bc523980fb476c990b982a46a9f36649097b8c02b590ae72a2bac4a7a790490b".hexToBytes(),
    *uriOut
  )
}
