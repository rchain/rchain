//scalapackage coop.rchain.rholang.mint

//requires Pos, RhoSpec

match (
  "047b43d6548b72813b89ac1b9f9ca67624a8b372feedd71d4e2da036384a3e1236812227e524e6f237cde5f80dbb921cac12e6500791e9a9ed1254a745a816fe1f".hexToBytes()
) {
  posPubKey => {
    new
      rl(`rho:registry:lookup`),
      stdlog(`rho:io:stdlog`),
      revAddressOps(`rho:rev:address`),
      posRevAddressCh, posVaultCh,
      RhoSpecCh, PoSCh, RevVaultCh, ListOpsCh,
      setup, prepareUser,
      test_make_pos_succeeds,
      test_close_block_finishes,
      test_bonding_succeeds,
      test_withdraw_succeeds,
      test_validator_is_paid_after_withdraw,
      test_multiple_bonding_succeeds,
      test_bonding_fails_if_deposit_fails,
      test_bonding_fails_if_already_bonded,
      test_bonding_fails_if_bond_too_small,
      test_pay_succeeds,
      test_refund_succeeds,
      test_commit_random,
      test_dont_pay_inactive_validators,
      test_bonding_failure,
      getPosBalance,
      closeBlock
    in {
      revAddressOps!("fromPublicKey", posPubKey, *posRevAddressCh) |
      rl!(`rho:rchain:revVault`, *RevVaultCh) |
      rl!(`rho:rchain:pos`, *PoSCh) |
      rl!(`rho:lang:listOps`, *ListOpsCh) |
      rl!(`rho:id:zphjgsfy13h1k85isc8rtwtgt3t9zzt5pjd5ihykfmyapfc4wt3x5h`, *RhoSpecCh) |
      for(@(_, RhoSpec) <- RhoSpecCh;
          @(_, PoS) <- PoSCh;
          @(_, RevVault) <- RevVaultCh;
          @posRevAddress <- posRevAddressCh;
          @(_, ListOps) <- ListOpsCh) {
        stdlog!("info", {"posRevAddress":posRevAddress}) |

        @RevVault!("findOrCreate", posRevAddress, *posVaultCh) |
        for(@(true, posVault) <- posVaultCh) {
          @RhoSpec!("testSuite", *setup,
            [
              ("PoS is created with empty bonds", *test_make_pos_succeeds),
              ("closeBlock finishes successfully", *test_close_block_finishes),
              ("bonding success", *test_bonding_succeeds),
              ("withdraw succeeds", *test_withdraw_succeeds),
              ("validator is paid after withdraw", *test_validator_is_paid_after_withdraw),
              ("bonding fails if deposit fails", *test_bonding_fails_if_deposit_fails),
              ("multiple bondings work", *test_multiple_bonding_succeeds),
              ("payment works", *test_pay_succeeds),
              ("payment refund works", *test_refund_succeeds),
              ("bonding fails if already bonded", *test_bonding_fails_if_already_bonded),
              ("bonding fails is bond is too small", *test_bonding_fails_if_bond_too_small),
              ("payment is not distributed to inactive validators", *test_dont_pay_inactive_validators),
              ("commited random matches its image", *test_commit_random)
            ]) |

          contract setup(_, retCh) = {
            retCh!({})
          } |

          contract prepareUser(@pk, retCh) = {
            stdlog!("info", ("preparing user ", pk)) |
            new makeDeployerId(`rho:test:deployerId:make`),
                deployerIdCh,
                revAddrCh,
                vaultCh in {
              revAddressOps!("fromPublicKey", pk.hexToBytes(), *revAddrCh) |
              makeDeployerId!("deployerId", pk.hexToBytes(), *deployerIdCh) |
              for (
                @revAddress <- revAddrCh;
                @deployerId <- deployerIdCh
              ) {
                @RevVault!("findOrCreate", revAddress, *vaultCh) |
                for (@(true, vault) <- vaultCh) {
                  retCh!(pk.hexToBytes(), revAddress, vault, deployerId)
                }
              }
            }
          } |

          contract test_make_pos_succeeds(rhoSpec, _, ackCh) = {
            new bondsCh, rewardsCh, rewardsZeroCh, rewardsZeroPred in {
              @PoS!("getBonds", *bondsCh) |
              @PoS!("getRewards", *rewardsCh) |
              for ( @bonds <- bondsCh; @rewards <- rewardsCh) {
                @ListOps!("forall", rewards.toList(), *rewardsZeroPred, *rewardsZeroCh) |
                contract rewardsZeroPred(@(_, reward), resultCh) = {
                  resultCh!(reward == 0)
                } |

                rhoSpec!("assertMany",
                  [
                    (bonds.size() >= 0, "some bonds map should be received"),
                    ((0, "==", rewards.size()), "the rewards map is initially empty"),
                    ((true, "== <-", *rewardsZeroCh), "no rewards yet")
                  ],
                  *ackCh
                )
              }
            }
          } |


          contract test_close_block_finishes(rhoSpec, _, ackCh) = {
            new resultCh in {
              closeBlock!(*resultCh) |
              for ( _ <- resultCh) {
                rhoSpec!("assertMany",
                  [
                    (true, "all good here")
                  ],
                  *ackCh
                )
              }
            }
          } |

          contract test_bonding_succeeds(rhoSpec, _, ackCh) = {
            new setupCh, retCh, initialPosBalanceCh, finalPosBalanceCh,
                initialUserBalanceCh, finalUserBalanceCh,
                bondsCh, rewardsCh in {
              prepareUser!("1111111111111111111111111111111111111111111111111111111111111111111111111111111111111111111111111111111111111111111111111111111111", *setupCh) |
              for (@user1PubKey, _, @user1Vault, @user1DeployerId <- setupCh) {
                getPosBalance!(*initialPosBalanceCh) |
                @user1Vault!("balance", *initialUserBalanceCh) |
                for(@initialPosBalance <- initialPosBalanceCh;
                    @initialUserBalance <- initialUserBalanceCh) {
                  @PoS!("bond", user1DeployerId, 100, *retCh) |
                  for (@(true, _) <- retCh) {
                    @PoS!("getBonds", *bondsCh) |
                    getPosBalance!(*finalPosBalanceCh) |
                    @user1Vault!("balance", *finalUserBalanceCh) |
                    @PoS!("getRewards", *rewardsCh) |
                    for(@finalPosBalance <- finalPosBalanceCh;
                        @finalUserBalance <- finalUserBalanceCh;
                        @bonds <- bondsCh;
                        @rewards <- rewardsCh) {
                      match bonds.get(user1PubKey) {
                        bondAmount => {
                          rhoSpec!("assertMany",
                            [
                              ((100, "==", bondAmount), "the new bond is expected in the final map"),
                              ((100, "==", initialUserBalance - finalUserBalance), "the user account decreases"),
                              ((100, "==", finalPosBalance - initialPosBalance), "the pos account increases"),
                              ((Nil, "==", rewards.get(user1PubKey)), "new validator reward should be zero")
                            ],
                            *ackCh
                          )
                        }
                      }
                    }
                  }
                }
              }
            }
          } |

          contract test_withdraw_succeeds(rhoSpec, _, ackCh) = {
            new setupCh, retCh,
                initialPosBalanceCh, finalPosBalanceCh,
                initialUserBalanceCh, finalUserBalanceCh,
                initialRewardsCh, finalRewardsCh,
                initialBondsCh, finalBondsCh,
                withdrawCh, closeBlockCh in {
              prepareUser!("aaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaa", *setupCh) |
              for (@user1PubKey, _, @user1Vault, @user1DeployerId <- setupCh) {
                getPosBalance!(*initialPosBalanceCh) |
                @user1Vault!("balance", *initialUserBalanceCh) |
                @PoS!("getBonds", *initialBondsCh) |
                @PoS!("getRewards", *initialRewardsCh) |
                for(@initialPosBalance <- initialPosBalanceCh;
                    @initialUserBalance <- initialUserBalanceCh;
                    @initialBonds <- initialBondsCh;
                    @initialRewards <- initialRewardsCh) {
                  @PoS!("bond", user1DeployerId, 100, *retCh) |
                  for ( @(true, _) <- retCh) {
                    @PoS!("withdraw", user1DeployerId, *withdrawCh) |
                    for (_ <- withdrawCh) {
                      closeBlock!(*closeBlockCh) |
                      for (_ <- closeBlockCh) {
                        @PoS!("getBonds", *finalBondsCh) |
                        getPosBalance!(*finalPosBalanceCh) |
                        @user1Vault!("balance", *finalUserBalanceCh) |
                        @PoS!("getRewards", *finalRewardsCh) |
                        for(@finalPosBalance <- finalPosBalanceCh;
                            @finalUserBalance <- finalUserBalanceCh;
                            @finalBonds <- finalBondsCh;
                            @finalRewards <- finalRewardsCh) {
                          rhoSpec!("assertMany",
                            [
                              ((finalBonds, "==", initialBonds), "bonds don't change"),
                              ((finalRewards, "==", initialRewards), "rewards don't change"),
                              ((finalUserBalance, "==", initialUserBalance), "user balancs doesn't change"),
                              ((finalPosBalance, "==", initialPosBalance), "pos balance doesn't change")
                            ],
                            *ackCh
                          )
                        }
                      }
                    }
                  }
                }
              }
            }
          } |

          contract test_validator_is_paid_after_withdraw(rhoSpec, _, ackCh) = {
            new setupCh, retCh0, retCh1,
                initialValidatorBalanceCh, finalValidatorBalanceCh,
                initialBondsCh, finalBondsCh,
                withdrawCh, closeBlockCh in {
              prepareUser!("bbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbb", *setupCh) |
              for (@validatorPubKey, _, @validatorVault, @validatorDeployerId <- setupCh) {
                @validatorVault!("balance", *initialValidatorBalanceCh) |
                @PoS!("getBonds", *initialBondsCh) |
                for(@initialValidatorBalance <- initialValidatorBalanceCh;
                    @initialBonds <- initialBondsCh) {
                  @PoS!("bond", validatorDeployerId, 100, *retCh0) |
                  for ( @(true, _) <- retCh0) {
                    closeBlock!(*closeBlockCh) |
                    for (_ <- closeBlockCh) {
                      prepareUser!("cccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc", *setupCh) |
                      for (@userPubKey, _, @userVault, @userDeployerId <- setupCh) {
                        @PoS!("chargeDeploy", userDeployerId, 100, *retCh0) |
                        @PoS!("refundDeploy", 0, *retCh1) |
                        for ( @(true, _) <- retCh0; @(true, _) <- retCh1){
                          closeBlock!(*closeBlockCh) |
                          for (_ <- closeBlockCh) {
                            @PoS!("withdraw", validatorDeployerId, *withdrawCh) |
                            for (_ <- withdrawCh) {
                              closeBlock!(*closeBlockCh) |
                              for (_ <- closeBlockCh) {
                                @validatorVault!("balance", *finalValidatorBalanceCh) |
                                @PoS!("getBonds", *finalBondsCh) |
                                for(@finalBonds <- finalBondsCh;
                                    @finalValidatorBalance <- finalValidatorBalanceCh) {
                                  rhoSpec!("assertMany",
                                    [
                                      ((finalBonds, "==", initialBonds), "bonds don't change"),
                                      ((46, "==", finalValidatorBalance-initialValidatorBalance ), "user balance changes accordingly")
                                    ],
                                    *ackCh
                                  )
                                }
                              }
                            }
                          }
                        }
                      }
                    }
                  }
                }
              }
            }
          } |
          contract test_multiple_bonding_succeeds(rhoSpec, _, ackCh) = {
            new setupCh, retCh, initialPosBalanceCh, finalPosBalanceCh,
                initialUser1BalanceCh, finalUser1BalanceCh,
                initialUser2BalanceCh, finalUser2BalanceCh in {
              prepareUser!("2222222222222222222222222222222222222222222222222222222222222222222222222222222222222222222222222222222222222222222222222222222222", *setupCh) |
              for (@user1PubKey, _, @user1Vault, @user1DeployerId <- setupCh) {
                getPosBalance!(*initialPosBalanceCh) |
                @user1Vault!("balance", *initialUser1BalanceCh) |
                for(@initialPosBalance <- initialPosBalanceCh;
                    @initialUser1Balance <- initialUser1BalanceCh) {
                  @PoS!("bond", user1DeployerId, 100, *retCh) |
                  for ( @(true, _) <- retCh) {
                    stdlog!("info", "first bond") |
                    prepareUser!("3333333333333333333333333333333333333333333333333333333333333333333333333333333333333333333333333333333333333333333333333333333333", *setupCh) |
                    for(@user2PubKey, _, @user2Vault, @user2DeployerId <- setupCh) {
                      @user2Vault!("balance", *initialUser2BalanceCh) |
                      for (@initialUser2Balance <- initialUser2BalanceCh) {
                        @PoS!("bond", user2DeployerId, 200, *retCh) |
                        for ( @(true, _) <- retCh) {

                          @PoS!("getBonds", *retCh) |
                          getPosBalance!(*finalPosBalanceCh) |
                          @user1Vault!("balance", *finalUser1BalanceCh) |
                          @user2Vault!("balance", *finalUser2BalanceCh) |
                          for(@finalPosBalance <- finalPosBalanceCh;
                              @finalUser1Balance <- finalUser1BalanceCh;
                              @finalUser2Balance <- finalUser2BalanceCh;
                              @bonds <- retCh) {
                            match (bonds.get(user1PubKey), bonds.get(user2PubKey)) {
                              (bondAmount1, bondAmount2) => {
                                rhoSpec!("assertMany",
                                  [
                                    ((100, "==", bondAmount1), "the user1 bond is expected in the final map"),
                                    ((200, "==", bondAmount2), "the user2 bond is expected in the final map"),
                                    ((100, "==", initialUser1Balance - finalUser1Balance), "the user1 account decreases"),
                                    ((200, "==", initialUser2Balance - finalUser2Balance), "the user2 account decreases"),
                                    ((300, "==", finalPosBalance - initialPosBalance), "the pos account increases"),
                                  ],
                                  *ackCh
                                )
                              }
                            }
                          }
                        }
                      }
                    }
                  }
                }
              }
            }
          } |

          contract test_pay_succeeds(rhoSpec, _, ackCh) = {
            new setupCh, closeBlockCh, retCh0, retCh1, initialPosBalanceCh, finalPosBalanceCh,
                initialUser1BalanceCh, finalUser1BalanceCh,
                initialRewardsCh, finalRewardsCh,
                computeDelta, deltaRewardsCh, deltaSumCh, sumDelta,
                isPositive, allRewardsPositiveCh
            in {
              prepareUser!("4444444444444444444444444444444444444444444444444444444444444444444444444444444444444444444444444444444444444444444444444444444444", *setupCh) |
              for (@validatorPubKey, _, _, @validatorDeployerId <- setupCh) {
                @PoS!("bond", validatorDeployerId, 100, *setupCh) |
                for (@(true, _) <- setupCh) {
                  closeBlock!(*closeBlockCh) |
                  prepareUser!("5555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555", *setupCh) |
                  for (@user1PubKey, _, @user1Vault, @user1DeployerId <- setupCh;
                       _ <- closeBlockCh) {
                    getPosBalance!(*initialPosBalanceCh) |
                    @user1Vault!("balance", *initialUser1BalanceCh) |
                    @PoS!("getRewards", *initialRewardsCh) |
                    for(@initialPosBalance <- initialPosBalanceCh;
                        @initialUser1Balance <- initialUser1BalanceCh;
                        @initialRewards <- initialRewardsCh) {
                      @PoS!("chargeDeploy", user1DeployerId, 100, *retCh0) |
                      @PoS!("refundDeploy", 0, *retCh1) |
                      for ( @(true, _) <- retCh0; @(true, _) <- retCh1) {
                        closeBlock!(*closeBlockCh) |
                        for (_ <- closeBlockCh) {
                          getPosBalance!(*finalPosBalanceCh) |
                          @user1Vault!("balance", *finalUser1BalanceCh) |
                          @PoS!("getRewards", *finalRewardsCh) |
                          for(@finalPosBalance <- finalPosBalanceCh;
                              @finalUser1Balance <- finalUser1BalanceCh;
                              @finalRewards <- finalRewardsCh) {
                            @ListOps!("fold", finalRewards.toList(), {}, *computeDelta, *deltaRewardsCh) |
                            contract computeDelta(@(pk, finalReward), @acc, resultCh) = {
                              resultCh!(acc.set(pk, finalReward - initialRewards.getOrElse(pk, 0)))
                            } |
                            for (@deltaRewards <- deltaRewardsCh) {
                              @ListOps!("forall", deltaRewards, *isPositive, *allRewardsPositiveCh) |
                              contract isPositive(@n, resultCh) = { resultCh!(n > 0)} |

                              @ListOps!("fold", deltaRewards.toList(), 0, *sumDelta, *deltaSumCh) |
                              contract sumDelta(@(_, delta), @acc, resultCh) = {
                                resultCh!(acc + delta)
                              } |

                              rhoSpec!("assertMany",
                                [
                                  ((100, "==", initialUser1Balance - finalUser1Balance), "the user account decreases"),
                                  ((100, "==", finalPosBalance - initialPosBalance), "the pos account increases"),
                                  ((19, "==", finalRewards.get(validatorPubKey)), "the validator's finalRewards is as expected"),
                                  //TODO use arbitrary precision numbers instead of long ints
                                  ((96, "== <-", *deltaSumCh), "the sum difference should equal the payment"),
                                  ((true, "== <-", *allRewardsPositiveCh), "the rewards can only grow")
                                ],
                                *ackCh
                              )
                            }
                          }
                        }
                      }
                    }
                  }
                }
              }
            }
          } |

          contract test_refund_succeeds(rhoSpec, _, ackCh) = {
            new prepareUserAck,
                bondAck,
                closeBlockAck,
                posBalanceCh,
                userBalanceCh,
                rewardsCh,
                chargeDeployAck,
                refundDeployAck,
                computeDelta, deltaRewardsCh, deltaSumCh, sumDelta,
                isPositive, allRewardsPositiveCh
            in {
              prepareUser!("dddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddd", *prepareUserAck) |
              for (@validatorPubKey, _, _, @validatorDeployerId <- prepareUserAck) {
                @PoS!("bond", validatorDeployerId, 100, *bondAck) |
                for (@(true, _) <- bondAck) {
                  closeBlock!(*closeBlockAck) |
                  prepareUser!("eeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeee", *prepareUserAck) |
                  for (@userPubKey, _, @userVault, @userDeployerId <- prepareUserAck; _ <- closeBlockAck) {
                    getPosBalance!(*posBalanceCh) |
                    @userVault!("balance", *userBalanceCh) |
                    @PoS!("getRewards", *rewardsCh) |
                    for(@initialPosBalance <- posBalanceCh;
                        @initialUserBalance <- userBalanceCh;
                        @initialRewards <- rewardsCh) {
                      @PoS!("chargeDeploy", userDeployerId, 100, *chargeDeployAck) |
                      @PoS!("refundDeploy", 50, *refundDeployAck) |
                      for ( @(true, _) <- chargeDeployAck; @(true, _) <- refundDeployAck) {
                        closeBlock!(*closeBlockAck) |
                        for (_ <- closeBlockAck) {
                          getPosBalance!(*posBalanceCh) |
                          @userVault!("balance", *userBalanceCh) |
                          @PoS!("getRewards", *rewardsCh) |
                          for(@finalPosBalance <- posBalanceCh;
                              @finalUserBalance <- userBalanceCh;
                              @finalRewards <- rewardsCh) {
                            @ListOps!("fold", finalRewards.toList(), {}, *computeDelta, *deltaRewardsCh) |
                            contract computeDelta(@(pk, finalReward), @acc, resultCh) = {
                              resultCh!(acc.set(pk, finalReward - initialRewards.getOrElse(pk, 0)))
                            } |
                            for (@deltaRewards <- deltaRewardsCh) {
                              @ListOps!("forall", deltaRewards, *isPositive, *allRewardsPositiveCh) |
                              contract isPositive(@n, resultCh) = {resultCh!(n > 0)} |

                              @ListOps!("fold", deltaRewards.toList(), 0, *sumDelta, *deltaSumCh) |
                              contract sumDelta(@(_, delta), @acc, resultCh) = {
                                resultCh!(acc + delta)
                              } |
                              rhoSpec!("assertMany",
                                [
                                  ((50, "==", initialUserBalance - finalUserBalance), "the user account decreases"),
                                  ((50, "==", finalPosBalance - initialPosBalance), "the pos account increases"),
                                  ((8, "==", finalRewards.get(validatorPubKey)), "the validator's finalRewards is as expected"),
                                  //TODO use arbitrary precision numbers instead of long ints
                                  ((48, "== <-", *deltaSumCh), "the sum difference should equal the payment"),
                                  ((true, "== <-", *allRewardsPositiveCh), "the rewards can only grow")
                                ],
                                *ackCh
                              )
                            }
                          }
                        }
                      }
                    }
                  }
                }
              }
            }
          } |

          contract test_bonding_fails_if_deposit_fails(rhoSpec, _, ackCh) = {
            test_bonding_failure!(*rhoSpec, "6666666666666666666666666666666666666666666666666666666666666666666666666666666666666666666666666666666666666666666666666666666666", 20000, "Bond deposit failed: Insufficient funds", *ackCh)
          } |

          contract test_bonding_fails_if_bond_too_small(rhoSpec, _, ackCh) = {
            test_bonding_failure!(*rhoSpec, "7777777777777777777777777777777777777777777777777777777777777777777777777777777777777777777777777777777777777777777777777777777777", -1, "Bond is less than minimum!", *ackCh)
          } |

          contract test_bonding_fails_if_already_bonded(rhoSpec, _, ackCh) = {
            new setupCh, initialBondsCh, bond1Ch, bond2Ch, bondsCh,
                finalRewardsCh, initialRewardsCh in {
              prepareUser!("8888888888888888888888888888888888888888888888888888888888888888888888888888888888888888888888888888888888888888888888888888888888", *setupCh) |
              for (@user1PubKey, _, _, @user1DeployerId <- setupCh) {
                @PoS!("bond", user1DeployerId, 150, *bond1Ch) |
                for ( @(result1, _) <- bond1Ch) {
                  @PoS!("getBonds", *initialBondsCh) |
                  @PoS!("getRewards", *initialRewardsCh) |
                  for ( @initialBonds <- initialBondsCh;
                        @initialRewards <- initialRewardsCh) {
                    @PoS!("bond", user1DeployerId, 150, *bond2Ch) |
                    for ( @(result2, msg2) <- bond2Ch) {
                      @PoS!("getBonds", *bondsCh) |
                      @PoS!("getRewards", *finalRewardsCh) |
                      rhoSpec!("assertMany",
                        [
                          ((true, "==", result1), "the first bond should succeed"),
                          ((false, "==", result2), "the second bond should fail"),
                          (("Public key is already bonded.", "==", msg2), "the message should be as expected"),
                          ((initialBonds, "== <-", *bondsCh), "the bonds map remains unchanged"),
                          ((initialRewards, "== <-", *finalRewardsCh), "the rewards map should not change")
                        ],
                        *ackCh
                      )
                    }
                  }
                }
              }
            }
          } |

          contract test_bonding_failure(rhoSpec, @pk, @transferAmount, @expectedMsg, ackCh) = {
            new setupCh, retCh, bondsCh,
                finalRewardsCh, initialRewardsCh in {
              prepareUser!(pk, *setupCh) |
              for (_, _, _, @deployerId <- setupCh) {
                @PoS!("getBonds", *bondsCh) |
                @PoS!("getRewards", *initialRewardsCh) |
                for ( @initialBonds <- bondsCh;
                      @initialRewards <- initialRewardsCh) {
                  @PoS!("bond", deployerId, transferAmount, *retCh) |

                  for ( @(result, msg) <- retCh) {
                    @PoS!("getBonds", *bondsCh) |
                    @PoS!("getRewards", *finalRewardsCh) |
                    rhoSpec!("assertMany",
                      [
                        ((expectedMsg, "==", msg), "the message should be as epected"),
                        ((false, "==", result), "the bond should fail"),
                        ((initialBonds, "== <-", *bondsCh), "the bonds map remains unchanged"),
                        ((initialRewards, "== <-", *finalRewardsCh), "the rewards map should not change")
                      ],
                      *ackCh
                    )
                  }
                }
              }
            }
          } |

          contract test_dont_pay_inactive_validators(rhoSpec, _, ackCh) = {
            new setupCh, closeBlockCh, retCh,
                finalRewardsCh
            in {
              prepareUser!("9999999999999999999999999999999999999999999999999999999999999999999999999999999999999999999999999999999999999999999999999999999999", *setupCh) |
              for (@validatorPubKey, _, _, @validatorDeployerId <- setupCh) {
                @PoS!("bond", validatorDeployerId, 100, *setupCh) |
                for (@(true, _) <- setupCh) {
                  prepareUser!("0000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000", *setupCh) |
                  for (@user1PubKey, _, @user1Vault, @user1DeployerId <- setupCh) {
                    @PoS!("chargeDeploy", user1DeployerId, 100, *retCh) |
                    for ( @(true, _) <- retCh) {
                      closeBlock!(*closeBlockCh) |
                      for (_ <- closeBlockCh) {
                        @PoS!("getRewards", *finalRewardsCh) |
                        for(@finalRewards <- finalRewardsCh) {
                          rhoSpec!("assertMany",
                            [
                              ((Nil, "==", finalRewards.get(validatorPubKey)), "the new validator should have no reward")
                            ],
                            *ackCh
                          )
                        }
                      }
                    }
                  }
                }
              }
            }
          } |

          contract getPosBalance(retCh) = {
            new accBalances, vaultsCh, posVaultBalanceCh in {
              contract accBalances(@(_, _, vault), @acc, resultCh) = {
                new balanceCh in {
                  @vault!("balance", *balanceCh) |
                  for (@balance <- balanceCh) {
                    resultCh!(acc + balance)
                  }
                }
              } |

              @posVault!("balance", *posVaultBalanceCh) |
              for (@posVaultBalance <- posVaultBalanceCh) {
                @PoS!("getActiveValidatorVaults", *vaultsCh) |
                for (@vaults <- vaultsCh) {
                  @ListOps!("fold", vaults, posVaultBalance, *accBalances, *retCh)
                }
              }
            }
          } |

          contract closeBlock(ackCh) = {
            new ackCh0, activeValidatorsCh, setBlockData(`rho:test:block:data:set`), blockDataSet in {
              @PoS!("closeBlock", *ackCh0) |
              @PoS!("getActiveValidators", *activeValidatorsCh) |
              for (@ack <- ackCh0; @activeValidators <- activeValidatorsCh) {
                // For testing we need one of the active validators to be the block sender
                setBlockData!("sender", activeValidators.nth(0), *blockDataSet) |
                for (_ <- blockDataSet) {
                  ackCh!(ack)
                }
              }
            }
          } |

          contract test_commit_random(rhoSpec, _, ackCh) = {
            new setupCh, closeBlockCh, retCh,
                finalRewardsCh, computeHash(`rho:crypto:keccak256Hash`), hash00Ch, hashFFCh
            in {
              prepareUser!("dddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddddd", *setupCh) |
              for (@validatorPubKey, _, _, @validatorDeployerId <- setupCh) {
                @PoS!("revealRandom", validatorDeployerId, "FF".hexToBytes(), *retCh) |

                computeHash!("00".hexToBytes(), *hash00Ch) |
                computeHash!("FF".hexToBytes(), *hashFFCh) |

                for (@result0 <- retCh;
                     @hash00 <- hash00Ch;
                     @hashFF <- hashFFCh) {
                  @PoS!("commitRandomImage", validatorDeployerId, hash00, *retCh) |
                  for (@result1 <- retCh) {
                    @PoS!("commitRandomImage", validatorDeployerId, hashFF, *retCh) |
                    for (@result2 <- retCh) {
                      @PoS!("revealRandom", validatorDeployerId, "FF".hexToBytes(), *retCh) |
                      for (@result3 <- retCh) {
                        @PoS!("revealRandom", validatorDeployerId, "00".hexToBytes(), *retCh) |
                        for (@result4 <- retCh) {
                          rhoSpec!("assertMany",
                            [
                              (((false, "Previously committed random image not found"), "==", result0), "commitRandom before commitRandomImage fails"),
                              ((true, "==", result1), "the first commitRandomImage is successful"),
                              (((false, "Image already committed"), "==", result2), "the second commitRandomImage for the same validator fails"),
                              (((false, "Previously committed image doesn't match the random number"), "==", result3), "the random value doesn't match the commited image stored by the first commitRandomImage"),
                              ((true, "==", result4), "the random value matches the commited image")
                            ],
                            *ackCh
                          )
                        }
                      }
                    }
                  }
                }
              }
            }
          }
        }
      }
    }
  }
}
